plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
}

group = 'org.utplsql'
version = '3.1.3-SNAPSHOT'
description = 'utPLSQL Gradle Plugin'
sourceCompatibility = '1.8'

repositories {
    jcenter()
    maven { url = 'https://packagecloud.io/utplsql/utplsql-java-api/maven2' }
    maven { url = 'https://maven.oracle.com' }
    maven { url = 'http://repo.maven.apache.org/maven2' }
}

dependencies {
//    implementation 'org.utplsql:java-api:3.1.2'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

gradlePlugin {
    plugins {
        utplsql {
            id = 'org.utplsql.gradle.plugin.utplsql'
            implementationClass = 'org.utplsql.gradle.plugin.UtPLSQLGradlePluginPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)


task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
